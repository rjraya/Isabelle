theory Miscellany
  imports Termination 
begin

text \<open>
 Here we list theorems that use intersting proof techniques which are not
 relevant to the final development of the theory itself.
\<close>

fun inf_reduction :: "com \<times> state \<Rightarrow> nat \<Rightarrow> com \<times> state" where 
"inf_reduction c\<^sub>0 0 = c\<^sub>0" |
"inf_reduction c\<^sub>0 (Suc n) = (SOME c. inf_reduction c\<^sub>0 n \<rightarrow>\<^sub>g c \<and> \<not> terminating c)"

lemma terminating_suc:
  assumes \<open>\<not> terminating c\<close>
  obtains c' where \<open>c \<rightarrow>\<^sub>g c'\<close> \<open>\<not> terminating c'\<close>
  by (meson assms terminating.intros)

lemma nontermination_implies_infinite_trace:
  assumes \<open>\<not> terminating c0\<close>
  shows  \<open>\<not> terminating (inf_reduction c0 n) 
    \<and> inf_reduction c0 n \<rightarrow>\<^sub>g inf_reduction c0 (Suc n)\<close>
  by (induct n,
     (simp, metis (mono_tags, lifting) terminating_suc assms exE_some)+) 

definition reachable :: "com \<Rightarrow> com set" where
"reachable c = {c'. \<exists> s t. (c,s) \<rightarrow>\<^sub>g* (c',t)}"

fun small_stepsn :: "com * state \<Rightarrow> nat \<Rightarrow> com * state \<Rightarrow> bool"
    ("_ \<rightarrow>\<^sub>g'(_') _" [55,0,55] 55) where
"(cs \<rightarrow>\<^sub>g(0) cs') = (cs' = cs)" |
"cs \<rightarrow>\<^sub>g(Suc n) cs'' = (\<exists>cs'. cs \<rightarrow>\<^sub>g cs' \<and> cs' \<rightarrow>\<^sub>g(n) cs'')"

lemma stepsn_if_star: "cs \<rightarrow>\<^sub>g* cs' \<Longrightarrow> \<exists>n. cs \<rightarrow>\<^sub>g(n) cs'"
proof(induction rule: star_induct1)
  case base
  then show ?case by (metis small_stepsn.simps(1))
next
  case (step y z)
  then show ?case by (metis small_stepsn.simps(2))
qed

lemma star_if_stepsn: "cs \<rightarrow>\<^sub>g(n) cs' \<Longrightarrow> cs \<rightarrow>\<^sub>g* cs'"
  apply(induction n arbitrary: cs) 
  by (simp, meson converse_rtranclp_into_rtranclp small_stepsn.simps(2))

lemma SKIP_starD: "(SKIP, s) \<rightarrow>\<^sub>g* (c,t) \<Longrightarrow> c = SKIP"
  by (metis converse_rtranclpE final_def final_iff_SKIP)

lemma reachable_SKIP: "reachable SKIP = {SKIP}"
by(auto simp: reachable_def dest: SKIP_starD)

lemma Assign_starD: "(x::=a, s) \<rightarrow>\<^sub>g* (c,t) \<Longrightarrow> c \<in> {x::=a, SKIP}"
  apply(induction "x::=a" "s" rule: star_induct)
  using SKIP_starD by auto

lemma reachable_Assign: "reachable (x::=a) = {x::=a, SKIP}"
  by(auto simp: reachable_def dest:Assign_starD)

lemma Seq_stepsnD: "(c1;; c2, s) \<rightarrow>\<^sub>g(n) (c', t) \<Longrightarrow>
  (\<exists>c1' m. c' = c1';; c2 \<and> (c1, s) \<rightarrow>\<^sub>g(m) (c1', t) \<and> m \<le> n) \<or>
  (\<exists>s2 m1 m2. (c1,s) \<rightarrow>\<^sub>g(m1) (SKIP,s2) \<and> (c2, s2) \<rightarrow>\<^sub>g(m2) (c', t) \<and> m1+m2 < n)"
proof(induction n arbitrary: c1 c2 s)
  case 0 thus ?case by auto
next
  case (Suc n)
  from Suc.prems obtain s' c12' where "(c1;;c2, s) \<rightarrow>\<^sub>g (c12', s')"
    and n: "(c12',s') \<rightarrow>\<^sub>g(n) (c',t)" by auto
  from this(1) show ?case
  proof
    assume "c1 = SKIP" "(c12', s') = (c2, s)"
    hence "(c1,s) \<rightarrow>\<^sub>g(0) (SKIP, s') \<and> (c2, s') \<rightarrow>\<^sub>g(n) (c', t) \<and> 0 + n < Suc n"
      using n by auto
    thus ?case by blast
  next
    fix c1' s'' assume 1: "(c12', s') = (c1';; c2, s'')" "(c1, s) \<rightarrow>\<^sub>g (c1', s'')"
    hence n': "(c1';;c2,s') \<rightarrow>\<^sub>g(n) (c',t)" using n by auto
    from Suc.IH[OF n'] show ?case
    proof
      assume "\<exists>c1'' m. c' = c1'';; c2 \<and> (c1', s') \<rightarrow>\<^sub>g(m) (c1'', t) \<and> m \<le> n"
        (is "\<exists> a b. ?P a b")
      then obtain c1'' m where 2: "?P c1'' m" by blast
      hence "c' = c1'';;c2 \<and> (c1, s) \<rightarrow>\<^sub>g(Suc m) (c1'',t) \<and> Suc m \<le> Suc n"
        using 1 by auto
      thus ?case by blast
    next
      assume "\<exists>s2 m1 m2. (c1',s') \<rightarrow>\<^sub>g(m1) (SKIP,s2) \<and>
        (c2,s2) \<rightarrow>\<^sub>g(m2) (c',t) \<and> m1+m2 < n" (is "\<exists>a b c. ?P a b c")
      then obtain s2 m1 m2 where "?P s2 m1 m2" by blast
      hence "(c1,s) \<rightarrow>\<^sub>g(Suc m1) (SKIP,s2) \<and> (c2,s2) \<rightarrow>\<^sub>g(m2) (c',t) \<and>
        Suc m1 + m2 < Suc n"  using 1 by auto
      thus ?case by blast
    qed
  qed
qed

corollary Seq_starD: "(c1;; c2, s) \<rightarrow>\<^sub>g* (c', t) \<Longrightarrow>
  (\<exists>c1'. c' = c1';; c2 \<and> (c1, s) \<rightarrow>\<^sub>g* (c1', t)) \<or>
  (\<exists>s2. (c1,s) \<rightarrow>\<^sub>g* (SKIP,s2) \<and> (c2, s2) \<rightarrow>\<^sub>g* (c', t))"
by(metis Seq_stepsnD star_if_stepsn stepsn_if_star)

lemma reachable_Seq: "reachable (c1;;c2) \<subseteq>
  (\<lambda>c1'. c1';;c2) ` reachable c1 \<union> reachable c2"
  by(auto simp: reachable_def image_def dest!: Seq_starD)

lemma reachable_If: "reachable (IF gcs FI) \<subseteq>
  {IF gcs FI} \<union> fold (\<lambda> l acc. l \<union> acc) (map reachable (snds gcs)) {}"
  apply(induction gcs)
  unfolding reachable_def
  oops

(*
lemma While_stepsnD: "(WHILE b DO c, s) \<rightarrow>(n) (c2,t) \<Longrightarrow>
  c2 \<in> {WHILE b DO c, IF b THEN c ;; WHILE b DO c ELSE SKIP, SKIP}
  \<or> (\<exists>c1. c2 = c1 ;; WHILE b DO c \<and> (\<exists> s1 s2. (c,s1) \<rightarrow>* (c1,s2)))"
proof(induction n arbitrary: s rule: less_induct)
  case (less n1)
  show ?case
  proof(cases n1)
    case 0 thus ?thesis using less.prems by (simp)
  next
    case (Suc n2)
    let ?w = "WHILE b DO c"
    let ?iw = "IF b THEN c ;; ?w ELSE SKIP"
    from Suc less.prems have n2: "(?iw,s) \<rightarrow>(n2) (c2,t)" by(auto elim!: WhileE)
    show ?thesis
    proof(cases n2)
      case 0 thus ?thesis using n2 by auto
    next
      case (Suc n3)
      then obtain iw' s' where "(?iw,s) \<rightarrow> (iw',s')"
        and n3: "(iw',s') \<rightarrow>(n3) (c2,t)"  using n2 by auto
      from this(1)
      show ?thesis
      proof
        assume "(iw', s') = (c;; WHILE b DO c, s)"
        with n3 have "(c;;?w, s) \<rightarrow>(n3) (c2,t)" by auto
        from Seq_stepsnD[OF this] show ?thesis
        proof
          assume "\<exists>c1' m. c2 = c1';; ?w \<and> (c,s) \<rightarrow>(m) (c1', t) \<and> m \<le> n3"
          thus ?thesis by (metis star_if_stepsn)
        next
          assume "\<exists>s2 m1 m2. (c, s) \<rightarrow>(m1) (SKIP, s2) \<and>
            (WHILE b DO c, s2) \<rightarrow>(m2) (c2, t) \<and> m1 + m2 < n3" (is "\<exists>x y z. ?P x y z")
          then obtain s2 m1 m2 where "?P s2 m1 m2" by blast
          with \<open>n2 = Suc n3\<close> \<open>n1 = Suc n2\<close>have "m2 < n1" by arith
          from less.IH[OF this] \<open>?P s2 m1 m2\<close> show ?thesis by blast
        qed
      next
        assume "(iw', s') = (SKIP, s)"
        thus ?thesis using star_if_stepsn[OF n3] by(auto dest!: SKIP_starD)
      qed
    qed
  qed
qed

lemma reachable_While: "reachable (WHILE b DO c) \<subseteq>
  {WHILE b DO c, IF b THEN c ;; WHILE b DO c ELSE SKIP, SKIP} \<union>
  (\<lambda>c'. c' ;; WHILE b DO c) ` reachable c"
apply(auto simp: reachable_def image_def)
by (metis While_stepsnD insertE singletonE stepsn_if_star)


theorem finite_reachable: "finite(reachable c)"
apply(induction c)
apply(auto simp: reachable_SKIP reachable_Assign
  finite_subset[OF reachable_Seq] finite_subset[OF reachable_If]
  finite_subset[OF reachable_While])
done
*)

end